// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        google()
        mavenCentral()

        maven { url 'https://maven.google.com/' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.1.3'

        // For FCM push notifications
        classpath 'com.google.gms:google-services:4.3.10'

        // Crashlytics
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.8.1'

        // Legal compliance: plugin to collect and display OSS licenses.
        classpath 'com.google.android.gms:oss-licenses-plugin:0.10.5'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

// This block encapsulates custom properties and makes them available to all
// modules in the project.
ext {
    version_code = gitVersionCode()
    version_name = gitVersionName()
}

allprojects {
    repositories {
        google()
        mavenCentral()

        maven { url 'https://maven.google.com/' }
        flatDir {
            dirs 'src/main/libs'
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

static def gitVersionCode() {
    def process = "git rev-list --count HEAD".execute()
    return process.text.toInteger()
}

static def gitVersionName() {
    def process = "git describe --tags".execute()
    // Remove trailing CR and remove leading 'v' as in 'v1.2.3'
    return process.text.trim().substring(1)
}
